 PREFIX lex: <http://data.bgs.ac.uk/ref/Lexicon/>
                PREFIX geochron: <http://data.bgs.ac.uk/ref/Geochronology/>
                PREFIX rock: <http://data.bgs.ac.uk/id/Lexicon/RockUnitRank/>
                PREFIX ext: <http://data.bgs.ac.uk/ref/Lexicon/Extended/>
                PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
                PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
                PREFIX xsd:   <http://www.w3.org/2001/XMLSchema#>

SELECT DISTINCT
?stripped_label
?stripped_notation
?lex 
?relationship
?stripped_upperLabel
?stripped_upperNotation
?upperLex  

 WHERE {
            ?lex lex:hasYoungestAgeValue ?minAge .
            ?lex lex:hasOldestAgeValue ?maxAge .
            ?lex lex:hasRockUnitRank ?rank .
            ?era geochron:minAgeValue ?eraMinAge .
            ?era geochron:maxAgeValue ?eraMaxAge .
            ?lex rdfs:label ?label .
            ?lex skos:notation ?notation .
            ?lex ext:upper ?upperLex .
            ?upperLex rdfs:label ?upperLabel .
            ?upperLex skos:notation ?upperNotation 

 BIND (STR(?notation)  AS ?stripped_notation) 
 BIND (STR(?label)  AS ?stripped_label) 
 BIND (STR(?upperNotation)  AS ?stripped_upperNotation) 
 BIND (STR(?upperLabel)  AS ?stripped_upperLabel) 
  BIND (("upper") AS ?relationship)
  
  FILTER ( xsd:double(?minAge) > xsd:double(?eraMinAge)
                && xsd:double(?maxAge) < xsd:double(?eraMaxAge)
                && ?era = <http://data.bgs.ac.uk/id/Geochronology/Division/J>
            )

  FILTER (?rank= rock:F)
       
}


ORDER BY DESC(?maxAge)
