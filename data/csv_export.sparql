 PREFIX lex: <http://data.bgs.ac.uk/ref/Lexicon/>
                PREFIX geochron: <http://data.bgs.ac.uk/ref/Geochronology/>
                PREFIX rock: <http://data.bgs.ac.uk/id/Lexicon/RockUnitRank/>
                PREFIX ext: <http://data.bgs.ac.uk/ref/Lexicon/Extended/>
                PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
                PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
                PREFIX xsd:   <http://www.w3.org/2001/XMLSchema#>
                PREFIX afn: <http://jena.apache.org/ARQ/function#>

SELECT DISTINCT
?lex_name
?lex_code
?lex_uri 
?boundary_type
?bounding_lex_name
?bounding_lex_code
?bounding_lex_uri  

 WHERE {
            ?lex_uri lex:hasYoungestAgeValue ?minAge .
            ?lex_uri lex:hasOldestAgeValue ?maxAge .
            ?lex_uri lex:hasRockUnitRank ?rank .
            ?era geochron:minAgeValue ?eraMinAge .
            ?era geochron:maxAgeValue ?eraMaxAge .
            ?lex_uri rdfs:label ?label .
            ?lex_uri skos:notation ?notation .
            ?lex_uri ?relationship ?bounding_lex_uri .
            ?bounding_lex_uri rdfs:label ?boundingLexLabel .
            ?bounding_lex_uri skos:notation ?boundingLexNotation 

 BIND (STR(?notation)  AS ?lex_code) 
 BIND (STR(?label)  AS ?lex_name) 
 BIND (STR(?boundingLexNotation)  AS ?bounding_lex_code) 
 BIND (STR(?boundingLexLabel)  AS ?bounding_lex_name) 
  BIND (afn:localname(?relationship) as ?boundary_type)

  FILTER (?relationship = ext:upper || ?relationship = ext:lower)
  
  FILTER ( xsd:double(?minAge) > xsd:double(?eraMinAge)
                && xsd:double(?maxAge) < xsd:double(?eraMaxAge)
                && ?era = <http://data.bgs.ac.uk/id/Geochronology/Division/J>
            )
  
  FILTER (?rank= rock:F)
       
}


ORDER BY DESC(?maxAge)
